
if (componentTag){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.enation.javashop.aspectjrt'

kapt {
    arguments {
        arg("moduleName", project.getName())
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        if (componentTag){
            applicationId "com.enation.javashop.android.component.welcome"
            multiDexEnabled true
        }
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        dataBinding {
            enabled = true
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/code'
        if(componentTag){
            main.manifest.srcFile 'src/main/ApkBuildDir/AndroidManifest.xml'
        }else{
            main.manifest.srcFile 'src/main/AndroidManifest.xml'
            main.java {
                exclude 'apkbuild/**'
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    /**
     * 单元测试
     */
    testCompile 'junit:junit:4.12'

    /**
     *  Android基础依赖库
     */
    compile 'com.android.support:design:26.+'
    compile 'com.android.support:support-v4:26.+'
    compile 'com.android.support:cardview-v7:26.+'

    /**
     * 公共库依赖
     */
    if(componentTag){
        compile project(':common')
    }else{
        provided project(':common')
    }


    /**
     * Dagger编译依赖
     */
    kapt 'com.google.dagger:dagger-compiler:2.7'

    /**
     * Kotlin依赖
     */
    kapt 'com.android.databinding:compiler:2.3.3'

    /**
     * 路由注解处理器
     */
    kapt "com.enation.geamtear:jrouter-compiler:$router_version"
}
